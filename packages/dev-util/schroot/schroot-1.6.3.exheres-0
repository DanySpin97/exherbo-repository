# Copyright 2010 Johannes Nixdorf <mixi@user-helfen-usern.de>
# Distributed under the terms of the GNU General Public License v2
# Based in part upon 'schroot-1.4.3' from Gentoo, which is:
#     Copyright 1999-2010 Gentoo Foundation

require debian-upstream [ suffix=".orig.tar.xz" ] pam

SUMMARY="Utility to execute commands in a chroot environment"
HOMEPAGE="http://packages.debian.org/source/sid/schroot"

LICENCES="GPL-3"
SLOT="0"
PLATFORMS="~amd64"
MYOPTIONS="lvm btrfs doc"

DEPENDENCIES="
    build+run:
        dev-cpp/cppunit[>=1.10.0]
        dev-libs/boost[>=1.42.0]
        dev-libs/lockdev
        sys-apps/util-linux[>=2.16]
        sys-devel/gettext
        sys-libs/pam
        btrfs? ( sys-fs/btrfs-progs )
        doc? (
            app-doc/doxygen
            media-gfx/graphviz
        )
        lvm? ( sys-fs/lvm2 )
"

# Tests require root
RESTRICT="test"

DEFAULT_SRC_CONFIGURE_OPTION_ENABLES=(
    'btrfs btrfs-snapshot'
    'doc doxygen'
    'lvm lvm-snapshot'
)

DEFAULT_SRC_CONFIGURE_PARAMS=(
    --disable-doxygen
    --disable-static
    --enable-block-device
    --enable-csbuild
    --enable-dchroot
    --enable-dchroot-dsa
    --enable-loopback
    --enable-pam
    --enable-shared
    --enable-union
    --enable-uuid
    --localstatedir=/var
    --with-bash-completion-dir=/usr/share/bash-completion
)

src_install() {
    default

    # the upstream pam.d file includes some debian pam.d files
    edo rm "${IMAGE}"/etc/pam.d/schroot
    pamd_mimic_system schroot auth account session

    keepdir \
        /etc/schroot/chroot.d \
        /var/lib/schroot/mount \
        /var/lib/schroot/session \
        /var/lib/schroot/unpack \
        /var/lib/schroot/union/overlay \
        /var/lib/schroot/union/underlay
}

